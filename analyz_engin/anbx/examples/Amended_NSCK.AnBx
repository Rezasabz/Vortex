Protocol: Amended_NSCK AnB # Verified

# AnBx to Proverif: compile with -pvtagfuntt option for full verification of all goals

Types:
    Agent A,B,s;
    Number NxNA,NxNB,NxNB0;
    SymmetricKey KxKAB;
    Function [Number -> Number] pre;
    Function [Agent,Agent ->* SymmetricKey] shk;
    
Knowledge:
    A: A,B,s,pre;
    B: B,A,s,pre;
    s: A,B,s;
    A,s share shk(A,s);
    B,s share shk(B,s)

Actions:
    # the vulnerable version has this first step, instead of the first three steps as follows
    # A -> s: A,B,NxNA

    A -> B: A
    B -> A: {|A,NxNB0|}shk(B,s)
    A -> s: A,B,NxNA,{|A,NxNB0|}shk(B,s)
    s -> A: {|NxNA,B,KxKAB,{|KxKAB,NxNB0,A|}shk(B,s)|}shk(A,s)
    A -> B: {|KxKAB,NxNB0,A|}shk(B,s)
    B -> A: {|NxNB|}KxKAB
    A -> B: {|pre(NxNB)|}KxKAB

Goals:
    A authenticates B on NxNB
    B authenticates A on NxNB
    NxNB secret between A,B
    B *->* A: NxNB
    A authenticates s on KxKAB
    B authenticates s on KxKAB    # fail 2 sessions in the vulnerable version